import com.github.jengelman.gradle.plugins.shadow.tasks.ShadowJar
import net.schmizz.sshj.SSHClient
import net.schmizz.sshj.transport.verification.PromiscuousVerifier

buildscript {
    repositories {
        maven { url = 'https://repo.spongepowered.org/repository/maven-public/' }
        mavenCentral()
        google()
        maven { url 'https://www.jitpack.io' }
    }
    dependencies {
        classpath 'org.spongepowered:mixingradle:0.7-SNAPSHOT'
        classpath 'com.hierynomus:sshj:0.38.0'
    }
}

plugins {
    id 'eclipse'
    id 'idea'
    id 'net.minecraftforge.gradle' version '[6.0.16,6.2)'
    id 'org.parchmentmc.librarian.forgegradle' version '1.+'
    id 'com.github.johnrengelman.shadow' version '8.1.1'
}

// Define the shade configuration and link it to implementation
configurations {
    shade
    implementation.extendsFrom shade
}

apply plugin: 'org.spongepowered.mixin'

group = mod_group_id
version = mod_version

base {
    archivesName = mod_id
}

java {
    toolchain.languageVersion = JavaLanguageVersion.of(17)
}

minecraft {
    accessTransformer = file('src/main/resources/META-INF/accesstransformer.cfg')
    mappings channel: 'parchment', version: '2023.09.03-1.20.1'

    copyIdeResources = true

    runs {
        configureEach {
            workingDirectory project.file('run')

            property 'forge.logging.markers', 'REGISTRIES'

            property 'forge.logging.console.level', 'debug'

            mods {
                "${mod_id}" {
                    source sourceSets.main
                }
            }
        }

        client {
            property 'forge.enabledGameTestNamespaces', mod_id
        }

        server {
            property 'forge.enabledGameTestNamespaces', mod_id
            args '--nogui'
        }

        gameTestServer {
            property 'forge.enabledGameTestNamespaces', mod_id
        }

        data {
            workingDirectory project.file('run-data')

            args '--mod', mod_id, '--all', '--output', file('src/generated/resources/'), '--existing', file('src/main/resources/')
        }
    }
}

mixin {
    add sourceSets.main, "${mod_id}.refmap.json"
    config "${mod_id}.mixins.json"
}

sourceSets.main.resources { srcDir 'src/generated/resources' }

repositories {
    maven { url "https://cursemaven.com" }
    maven { url 'https://jitpack.io' }
    maven {
        name = "PaperMC"
        url = "https://repo.papermc.io/repository/maven-public/"
    }
    mavenCentral()
}

dependencies {
    minecraft "net.minecraftforge:forge:${minecraft_version}-${forge_version}"

    implementation fg.deobf("curse.maven:timeless-and-classics-zero-1028108:6654541")
    compileOnly fg.deobf("curse.maven:tacz-durability-1065328:6633587")
    compileOnly "net.luckperms:api:5.4"
    compileOnly("io.papermc.paper:paper-api:1.20.1-R0.1-SNAPSHOT")

    minecraftLibrary "com.j256.ormlite:ormlite-core:6.1"
    minecraftLibrary "com.j256.ormlite:ormlite-jdbc:6.1"
    minecraftLibrary 'org.xerial:sqlite-jdbc:3.41.2.2'
    compileOnly fileTree(dir:"libs", include:"LuckPerms-Forge-5.4.88.jar")
    implementation fileTree(dir:"libs", include:"*.jar", exclude:"LuckPerms-Forge-5.4.88.jar" )

    compileOnly 'org.projectlombok:lombok:1.18.30'
    annotationProcessor 'org.projectlombok:lombok:1.18.30'

    annotationProcessor 'org.spongepowered:mixin:0.8.5:processor'
}

tasks.named('processResources', ProcessResources).configure {
    var replaceProperties = [minecraft_version   : minecraft_version, minecraft_version_range: minecraft_version_range,
                             forge_version       : forge_version, forge_version_range: forge_version_range,
                             loader_version_range: loader_version_range,
                             mod_id              : mod_id, mod_name: mod_name, mod_license: mod_license, mod_version: mod_version,
                             mod_authors         : mod_authors, mod_description: mod_description,]

    inputs.properties replaceProperties

    filesMatching(['META-INF/mods.toml', 'pack.mcmeta']) {
        expand replaceProperties + [project: project]
    }
}

// Example for how to get properties into the manifest for reading at runtime.
tasks.named('jar', Jar).configure {
    manifest {
        attributes(["Specification-Title"     : mod_id,
                    "Specification-Vendor"    : mod_authors,
                    "Specification-Version"   : "1",
                    "Implementation-Title"    : project.name,
                    "Implementation-Version"  : project.jar.archiveVersion,
                    "Implementation-Vendor"   : mod_authors,
                    "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")])
    }
    // We let shadowJar handle finalization manually in afterEvaluate
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'
}

// Tell ForgeGradle to create a reobf task for shadowJar
reobf {
    shadowJar {
    }
}

// Configure the Shadow task
tasks.named('shadowJar', ShadowJar).configure {
    archiveClassifier = ''
    configurations = [project.configurations.shade]
    relocate 'com.j256.ormlite', "${project.group}.shadow.ormlite"
}

// --- FIX: Use afterEvaluate to prevent "Task not found" errors ---
afterEvaluate {
    // 1. Ensure shadowJar runs after the standard jar
    tasks.shadowJar.dependsOn tasks.jar

    // 2. Ensure reobfShadowJar (created by the reobf block above) runs after shadowJar
    // This is the specific task ForgeGradle creates for shadowed jars
    if (tasks.findByName('reobfShadowJar')) {
        tasks.reobfShadowJar.mustRunAfter tasks.shadowJar
        tasks.build.dependsOn tasks.reobfShadowJar
    }

    // 3. Disable the standard reobfJar since we are using shadow
    if (tasks.findByName('reobfJar')) {
        tasks.reobfJar.enabled = false
    }
}

tasks.register('uploadJarToPterodactyl') {
    onlyIf { uploadJarToSFTP.toBoolean() }

    // Depends on the REOBFUSCATED shadow jar task
    dependsOn 'reobfShadowJar'

    doLast {
        // Grab the file from shadowJar
        def jarFile = tasks.shadowJar.archiveFile.get().asFile

        println "Prepare to upload: ${jarFile.name}"

        SSHClient ssh = new SSHClient()
        ssh.addHostKeyVerifier(new PromiscuousVerifier())
        ssh.connect(sftpHost as String, sftpPort as Integer)
        ssh.authPassword(sftpUser, sftpPassword)

        def sftp = ssh.newSFTPClient()
        sftp.put(jarFile.absolutePath, sftpPath + jarFile.name)
        sftp.close()
        ssh.disconnect()

        println "✔ Upload réussi vers Pterodactyl."
    }
}

build.finalizedBy('uploadJarToPterodactyl')